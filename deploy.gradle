buildscript {
    repositories {
        maven { url "http://eaglesakura.github.io/maven/" }
        jcenter()
    }

    dependencies {
        classpath "com.eaglesakura:plugin-ci-support:1.1.196"
    }
}

apply plugin: 'com.eaglesakura.ci-support'
apply plugin: 'deploygate'
apply from: "private/private.gradle"

private String getDeployMessage() {
    try {
        return file("deploy-log.txt").text;
    } catch (Throwable e) {
        return "";
    }
}

/**
 * フレーバー指定でapkを検索
 * @return 検索したAPKファイル or null
 */
private File getApk(String app, String flavor, String type) {
    File dir = file("ci-release/${app}/apk/");
    for (File file : dir.listFiles()) {
        println "check ${file.name}"
        if (flavor != null) {
            if ("${app}-${flavor}-${type}.apk".toString().equals(file.name)) {
                return file;
            }
        } else {
            if ("${app}-${type}.apk".toString().equals(file.name)) {
                return file;
            }
        }
    }
    return null;
}

deploygate {

    userName = DEPLOYGATE_USER_NAME;
    token = DEPLOYGATE_API_KEY;

    apks {
        /**
         * 本番バージョン
         */
        release {
            sourceFile = getApk("app", null, "release");
            message = "for Release App ${eglibrary.ci.buildDate}\n${getDeployMessage()}";
            releaseNote = "for Develop App | auto deploy | ${eglibrary.ci.buildDate}";
        }
    }
}

def APPLICATION_ID = "org.andriders.plugin.sasara"

/**
 * アルファ版: APKと更新情報をアップロードする
 */
task uploadGooglePlayAlphaVersion(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = APPLICATION_ID;
    track = "alpha";
    apk = getApk("app", null, "release")
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL;
    p12 = GOOGLEPLAY_DEPLOY_P12_FILE
}

/**
 * ベータ版: APKと更新情報をアップロードする
 */
task uploadGooglePlayBetaVersion(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = APPLICATION_ID;
    track = "beta";
    apk = getApk("app", null, "release")
    apkListings = file("googleplay/beta")
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL;
    p12 = GOOGLEPLAY_DEPLOY_P12_FILE
}

/**
 * 製品版: APKと更新情報をアップロードする
 */
task uploadGooglePlayReleaseVersion(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = APPLICATION_ID;
    track = "production";
    apk = getApk("app", null, "release")
    apkListings = file("googleplay/production")
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL;
    p12 = GOOGLEPLAY_DEPLOY_P12_FILE
}

/**
 * GooglePlay登録情報を送信する
 */
task uploadGooglePlayListings(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = APPLICATION_ID;
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL;
    listings = file("googleplay/production")
    p12 = GOOGLEPLAY_DEPLOY_P12_FILE
}

